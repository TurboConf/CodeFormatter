//@script_name CodeFormatter
//@script_display_name Форматировать модуль Стандарты 1С
//@script_description Более интеллектуальный вариант форматирования. Удаляет пустые строки, добавляет строки обрамляем конструкции, делает умные сдвиги
//@script_author SUshakov
//@script_hotkey NONE ФорматироватьМодульПоСтандарту
//@retain_clipboard 1
//@enterprise_mode 0
//@hide_actions 0
//@turbomenu 1
//@turbobutton 1
//@script_version 1

#Область ПрограммныйИнтерфейс

Процедура ФорматироватьМодульПоСтандарту() Экспорт

	ПозицияКурсора = 0;
	ВыделенныйТекст = "";

	ТК = Новый ТурбоКонф;
	ТК.ОтжатьМодификаторы();

	ТК.ПолучитьТекстМодуля(ВыделенныйТекст, ПозицияКурсора);

	СоздатьКаталогНастроек("settings/CodeFormatter");
	ИмяФайлаНастроек = "settings/CodeFormatter/settings.json";

	ФайлНастроек = Новый Файл(ИмяФайлаНастроек);
	
	Если ФайлНастроек.Существует() Тогда

		НастройкиФорматирования = ЗначениеИзФайлаJSON(ИмяФайлаНастроек);

	Иначе

		НастройкиФорматирования = Новый Структура;
		НастройкиФорматирования.Вставить("УстанавливатьОтступы", Истина);
		НастройкиФорматирования.Вставить("УстанавливатьСдвиги", Истина);
		НастройкиФорматирования.Вставить("ПриводитьККаноническомуНаписанию", Истина);
		НастройкиФорматирования.Вставить("УстановливатьПробелыДляМатематическихОператоров", Истина);
		
		СохранитьВJSON(НастройкиФорматирования, ИмяФайлаНастроек);

	КонецЕсли;	

	Форматирование = ЗагрузитьСценарий("scripts/CodeFormatter/CoreCodeFormatter.os");

	Форматирование.ФорматироватьМодуль(ВыделенныйТекст, НастройкиФорматирования);

	ТК.ВставитьТекст(ВыделенныйТекст);
	ТК.ОтжатьМодификаторы();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьКаталогНастроек(Каталог)

	Файл = Новый Файл(Каталог);

	Если НЕ Файл.Существует() Тогда
		СоздатьКаталог(Каталог);
	КонецЕсли;	

КонецПроцедуры

Функция ЗначениеИзФайлаJSON(ИмяФайла)
	
	Файл = Новый Файл(ИмяФайла);
	Если НЕ Файл.Существует() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Чтение = Новый ЧтениеJSON;
	Чтение.ОткрытьФайл(ИмяФайла);
	Данные = ПрочитатьJSON(Чтение, Ложь);
	Чтение.Закрыть();
	
	ОсвободитьОбъект(Чтение);
	
	Возврат Данные;
	
КонецФункции

Процедура СохранитьВJSON(ОбъектСохранения, ИмяФайла)
	
	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.ОткрытьФайл(ИмяФайла, "windows-1251", , Новый ПараметрыЗаписиJSON( , Символы.Таб));
	ЗаписатьJSON(ЗаписьJSON, ОбъектСохранения);
	ЗаписьJSON.Закрыть();
	
	ОсвободитьОбъект(ЗаписьJSON);
	
КонецПроцедуры

#КонецОбласти